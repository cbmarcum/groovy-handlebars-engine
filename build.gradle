/* build with:
* make sure to use java 21 and groovy 4.0
*
* ./gradlew publishMyLibraryPublicationToMyRepository
*
* * publish manually at:
* https://central.sonatype.com/publishing
* or to publish locally
* ./gradlew publishToMavenLocal
*
* to update this wrapper version
* $ gradle wrapper --gradle-version 8.5 --distribution-type all
*
*/

plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'

}


version = '0.3.0-SNAPSHOT'
group = 'net.codebuilders'
archivesBaseName = 'groovy-handlebars-engine'
description = 'A Handlebars template engine for Apache Groovy.'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

groovydoc {
    docTitle = "Groovy Handlebars Engine API"
    header = "Code Builders, LLC" // doesn't seem to work
    footer = "Generated documentation ${version}"
    windowTitle = docTitle
    use = true // create class and package usage pages
    classpath = sourceSets.main.output + sourceSets.main.compileClasspath.filter {
        !it.name.contains('handlebars')
    }

}

java {
    withJavadocJar()
    withSourcesJar()
}

ext {
    groovyVersion = "4.0.29"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.groovy:groovy:${groovyVersion}"
    implementation "org.apache.groovy:groovy-templates:${groovyVersion}"

    implementation "com.github.jknack:handlebars:4.4.0" // min java 17

    testImplementation "org.spockframework:spock-core:2.4-M6-groovy-4.0"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

}

// Disable the standard javadoc task since we're using groovydoc
javadoc.enabled = false

tasks.named('javadocJar') {
    dependsOn groovydoc
    from groovydoc.destinationDir
    archiveClassifier = 'javadoc'
}

tasks.withType(Test).configureEach {
    outputs.upToDateWhen { false } // always run tests
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStackTraces true
    }
}

// publishing and signing
// guide: https://docs.gradle.org/current/userguide/publishing_setup.html
// ref: https://github.com/cbmarcum/mybusiness/blob/master/build.gradle

publishing {
    publications {
        myLibrary(MavenPublication) {
            artifactId = 'groovy-handlebars-engine'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Groovy Handlebars Engine API'
                packaging = 'jar'
                description = 'A Handlebars template engine for Apache Groovy.'
                url = 'http://codebuilders.net'
                inceptionYear = '2025'

                scm {
                    url = 'https://github.com/cbmarcum/groovy-handlebars-engine'
                    developerConnection = 'https://github.com/cbmarcum/groovy-handlebars-engine'
                    connection = 'https://github.com/cbmarcum/groovy-handlebars-engine'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Carl Marcum'
                        email = 'carl.marcum@codebuilders.net'
                        url = 'https://codebuilders.net'
                        organization = 'Code Builders, LLC'
                        organizationUrl = 'https://codebuilders.net'

                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = layout.buildDirectory.dir("repo")
        }

    }

}

signing {
    sign publishing.publications.myLibrary
}